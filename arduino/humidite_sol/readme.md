# Atelier express de programmation Arduino <!-- omit in toc -->

![alt text](<assets/DALL¬∑E 2024-04-03 15.15.16.webp>)

<!-- TODO : Peaufiner la fin -->
- [Introduction](#introduction)
- [√âtape 1 : Survol de l'environnement de travail](#√©tape-1--survol-de-lenvironnement-de-travail)
  - [√âl√©ments importants de l'interface](#√©l√©ments-importants-de-linterface)
- [√âtape 2 : Observation du mat√©riel et du montage](#√©tape-2--observation-du-mat√©riel-et-du-montage)
- [√âtape 3 : Observation du montage](#√©tape-3--observation-du-montage)
  - [Fonctionnement du capteur d'eau](#fonctionnement-du-capteur-deau)
- [√âtape 4 : Brancher l'appareil](#√©tape-4--brancher-lappareil)
- [√âtape 5 : T√©l√©verser le programme](#√©tape-5--t√©l√©verser-le-programme)
- [√âtape 6 : √âtude du code](#√©tape-6--√©tude-du-code)
  - [Partie du haut - Les d√©clarations des constantes et variables](#partie-du-haut---les-d√©clarations-des-constantes-et-variables)
  - [Configuration initiale](#configuration-initiale)
  - [Boucle principale](#boucle-principale)
  - [Fonction `tacheHumidite()`](#fonction-tachehumidite)
- [fonction tacheLumiere()](#fonction-tachelumiere)
- [√âtape 7 : T√©l√©verser le programme](#√©tape-7--t√©l√©verser-le-programme)
- [√âtape 8 : Observer le fonctionnement](#√©tape-8--observer-le-fonctionnement)
- [Conclusion](#conclusion)

# Introduction
Dans cet atelier express d'environ 30 minutes, vous allez apprendre √† programmer une puce sur un circuit imprim√© pour r√©aliser un instrument de mesure d'humidit√© du sol. Nous allons utiliser une carte qui s'appelle un Arduino. Nous allons √©galement utiliser le logiciel Arduino IDE pour √©crire et t√©l√©verser le programme sur la puce.

**Cet activit√© r√©sume assez bien la premi√®re ann√©e du programme des Techniques de l'informatique du C√©gep.** En effet dans le programme, les √©tudiants apprennent les fondamentaux de la programmation, l'environnement de travail ainsi que le mat√©riel. Ils apprennent √©galement √† r√©soudre des probl√®mes.

√âvidemment, √©tant une activit√© de tr√®s courte dur√©e et que nous ne pouvons pas nous permettre de perdre du temps, nous allons utiliser un programme d√©j√† √©crit. Nous allons nous concentrer sur la compr√©hension du programme et sur la mani√®re de le t√©l√©verser sur la puce.


# √âtape 1 : Survol de l'environnement de travail
- Vous remarquez que nous sommes sur des ordinateurs Apple
- L'environnement est un peu diff√©rent des ordinateurs Windows ou Chromebook
- Vous avez un logiciel qui s'appelle Arduino IDE
  - Celui devrait √™tre d√©j√† ouvert
  - Si ce n'est pas le cas, recherchez l'ic√¥ne ci-contre dans la barre des applications et cliquez dessus
  
    <img src="assets/Arduino_IDE_logo.svg.png" width=50/>

---

L'interface devrait √™tre tr√®s similaire √† celui-ci

![alt text](assets/Arduino_IDE_base.png)

---

## √âl√©ments importants de l'interface

![alt text](assets/Arduino_IDE_annote.png)

- Dans l'encadr√© rouge
  - On retrouve un bouton repr√©senter par un crochet. Il s'agit du bouton `V√©rifier` qui permet de faire la v√©rification du code si celui-ci est valide
  - Il y a aussi un bouton avec une fl√®che vers la droite. Il s'agit du bouton `T√©l√©verser` qui permet de t√©l√©verser le code sur la carte √©lectronique
- Dans l'encadr√© jaune, il s'agit de la liste de s√©lection de la carte √©lectronique. Dans notre cas, il devrait d√©j√† √™tre s√©lectionn√© sur `Arduino Mega...`
- L'encadr√© bleu √† droite sont le traceur s√©rie et le moniteur s√©rie. C'est ici que nous allons voir les donn√©es envoy√©es par la carte √©lectronique.

# √âtape 2 : Observation du mat√©riel et du montage

![alt text](assets/kit_arduino.jpg)

√Ä votre disposition, vous avez un kit pr√©mont√© contenant les √©l√©ments suivants:
- Une carte Arduino Mega
- Un capteur d'humidit√© du sol
- Une DEL avec une r√©sistance
- Un c√¢ble USB pour connecter la carte √† un ordinateur

> **Question** : √ätes-vous capable d'identifier les diff√©rents √©l√©ments du kit?

# √âtape 3 : Observation du montage
- Observez le montage
- Trouvez la plaque prinpale
  - Celle-ci se nomme l'Arduino Mega
  - Il s'agit du cerveau de l'appareil
- Trouvez la DEL et la r√©sistance
  - La DEL est la petite lumi√®re
  - La r√©sistance est la petite composante √©lectronique avec des bandes de couleurs 
- Trouvez le capteur d'eau
  - Il s'agit d'une plaquette √©lectronique avec des bandes m√©talliques


![alt text](assets/del_resistance.jpg)

![alt text](assets/capteur_eau.jpg)

## Fonctionnement du capteur d'eau
- Le capteur d'eau est un capteur d'humidit√© du sol
- Il est compos√© de plusieurs bandes m√©talliques
- Chaque bande est alternativement connect√©e √† une anode et une cathode
- Le capteur envoie un signal √©lectrique qui varie en fonction de l'humidit√© du sol
- Plus le sol est humide, plus le signal est fort, car il y aura moins de r√©sistance
 
![alt text](assets/Water-Level-Sensor-Working.gif)

- L'Arduino pourra lire ce signal et le convertir en une valeur num√©rique

# √âtape 4 : Brancher l'appareil
- Si ce n'est d√©j√† fait, branchez le c√¢ble USB √† l'ordinateur
- Dans la liste de s√©lection de la carte √©lectronique, assurez-vous que `Arduino Mega...` est s√©lectionn√© sinon s√©lectionnez-le

# √âtape 5 : T√©l√©verser le programme

- Vous devriez avoir du code de base dans l'Arduino IDE
- S√©lectionnez tout le code et effacez-le
- Lancez le navigateur Firefox et allez √† l'adresse qui suit :
  
<big><b>üëâ [tinyurl.com/soleau24](http://tinyurl.com/soleau24) üëà</b></big>

- Copiez le code dans qui est dans le navigateur
- Collez le code dans l'Arduino IDE

![alt text](assets/Arduino_IDE_code.png)

# √âtape 6 : √âtude du code

Voici le code complet que vous avez coll√© dans l'Arduino IDE

```ino
// Constante pour les broches
#define MOIST_PIN A0
#define ON_PIN 2
#define LED_PIN 4

// Variables globales
int fautLire = true; // Drapeau pour la lecture
int humVal = 0; // Valeur lue de l'humidit√©

// Configuration initiale
// Code qui s'ex√©cute une seule fois
// aussi que l'√©lectricit√© est pr√©sente
void setup() {
  // put your setup code here, to run once:
  Serial.begin(9600);
  pinMode(ON_PIN, OUTPUT);
  pinMode(LED_PIN, OUTPUT);
}

// Code qui s'ex√©cute en boucle
// √† tant qu'il y a de l'√©lectricit√©
void loop() {
  tacheHumidite();
  tacheLumiere();
}

// Code pour allumer la lumi√®re
// si l'humidit√© d√©passe une valeur
void tacheLumiere(){
  // Si la valeur de humVal est plus
  // grand que X, √©crire la valeur
  // Y sur la broche
  if (humVal > 150) {
    analogWrite(LED_PIN, 255);
  }
  else if (humVal > 100) {
    analogWrite(LED_PIN, 128);
  }
  else if (humVal > 50) {
    analogWrite(LED_PIN, 64);
  } 
  else {
    digitalWrite(LED_PIN, LOW);
  }
}

// Code pour faire la lecture du
// taux d'humidit√©
// Objectif : On veut lire le niveau d'humidit√©
// √† toutes les 5 secondes
void tacheHumidite(){
  // D√©claration des variables
  // n√©cessaires √† la t√¢che
  static unsigned long derniereActivation = 0;
  static unsigned long nextRead = 0;
  const unsigned int freqActivation = 5000;
  const int lireApres = 10;

  // Si le drapeau de lecture est √† vrai
  // on va effectuer la lecture et
  // descendre le drapeau
  if (fautLire) {
    if (millis() > nextRead)
    {
      humVal = analogRead(MOIST_PIN); // On fait la lecture de la valeur
      Serial.println(humVal); // On envoie la valeur √† l'ordinateur

      digitalWrite (ON_PIN, LOW); // On retire l'√©lectricit√© au module
      fautLire = false; // On 'descend' le drapeau
    }
  }

  // Si le temps actuel moins la derni√®re fois est plus
  // grand que le temps d'activation, on fait le code
  // dans le bloc
  if (millis() - derniereActivation > freqActivation) {
    nextRead = millis() + lireApres;
    derniereActivation = millis();
    digitalWrite (ON_PIN, HIGH);
    fautLire = true;
  }
}
```

## Partie du haut - Les d√©clarations des constantes et variables

```ino
// Constante pour les broches
#define MOIST_PIN A0
#define ON_PIN 2
#define LED_PIN 4

// Variables globales
int fautLire = true; // Drapeau pour la lecture
int humVal = 0; // Valeur lue de l'humidit√©
```

- En math√©matiques, une constante est une valeur qui ne change pas
  - En programmation, c'est la m√™me chose
- En math√©matiques, les variables sont repr√©sent√©es par des lettres
  - En programmation, on utilise des noms de variables (mot)
- Les variables sont des valeurs qui peuvent changer

---

## Configuration initiale

```ino
// Configuration initiale
// Code qui s'ex√©cute une seule fois
// aussi que l'√©lectricit√© est pr√©sente
void setup() {
  // put your setup code here, to run once:
  Serial.begin(9600);
  pinMode(ON_PIN, OUTPUT);
  pinMode(LED_PIN, OUTPUT);
}
```

- La fonction `setup()` est une fonction sp√©ciale qui s'ex√©cute une seule fois aussit√¥t que l'√©lectricit√© est pr√©sente
- Lorsque la ligne d√©bute avec `void`, cela signifie que c'est une fonction

## Boucle principale

```ino
// Code qui s'ex√©cute en boucle
// √† tant qu'il y a de l'√©lectricit√©
void loop() {
  tacheHumidite();
  tacheLumiere();
}
```

- La fonction `loop()` est une fonction sp√©ciale qui s'ex√©cute en boucle tant qu'il y a de l'√©lectricit√© et elle ne s'arr√™te jamais
- On pourrait laisser l'appareil fonctionner pendant des jours, des mois, des ann√©es sans arr√™t
- La boucle est r√©p√©t√©e des milliers de fois par seconde!
- Dans celle-ci, il y a deux lignes qui appellent deux fonctions `tacheHumidite()` et `tacheLumiere()`
- Ces fonctions sont appel√©es s√©quentiellement, c'est-√†-dire l'une apr√®s l'autre

## Fonction `tacheHumidite()`

```ino
// Code pour faire la lecture du
// taux d'humidit√©
// Objectif : On veut lire le niveau d'humidit√©
// √† toutes les 5 secondes
void tacheHumidite(){
  // D√©claration des variables
  // n√©cessaires √† la t√¢che
  static unsigned long derniereActivation = 0;
  static unsigned long nextRead = 0;
  const unsigned int freqActivation = 5000;
  const int lireApres = 10;

  // Si le drapeau de lecture est √† vrai
  // on va effectuer la lecture et
  // descendre le drapeau
  if (fautLire) {
    if (millis() > nextRead)
    {
      humVal = analogRead(MOIST_PIN); // On fait la lecture de la valeur
      Serial.println(humVal); // On envoie la valeur √† l'ordinateur

      digitalWrite (ON_PIN, LOW); // On retire l'√©lectricit√© au module
      fautLire = false; // On 'descend' le drapeau
    }
  }

  // Si le temps actuel moins la derni√®re fois est plus
  // grand que le temps d'activation, on fait le code
  // dans le bloc
  if (millis() - derniereActivation > freqActivation) {
    nextRead = millis() + lireApres;
    derniereActivation = millis();
    digitalWrite (ON_PIN, HIGH);
    fautLire = true;
  }
}
```

- Cette fonction est la plus complexe du programme
- Elle est responsable de la lecture de l'humidit√© du sol et envoie la valeur lue √† l'ordinateur
- Les seules lignes qui sont responsables de la lecture de l'humidit√© sont les suivantes
  - `humVal = analogRead(MOIST_PIN);`
  - `Serial.println(humVal);`
- Alors pourquoi, il y a autant de code?
- Pour plusieurs raisons
  - Le capteur d'eau est tr√®s sensible √† la corrosion. En effet, √† chaque fois qu'il est aliment√©, il se corrode.
  - On veut lire l'humidit√© √† toutes les 5 secondes, car le sol ne change pas rapidement
  - On veut allumer et √©teindre le capteur d'eau pour √©conomiser de l'√©nergie

# fonction tacheLumiere()

```ino
// Code pour allumer la lumi√®re
// si l'humidit√© d√©passe une valeur
void tacheLumiere(){
  // Si la valeur de humVal est plus
  // grand que X, √©crire la valeur
  // Y sur la broche
  if (humVal > 150) {
    analogWrite(LED_PIN, 255);
  }
  else if (humVal > 100) {
    analogWrite(LED_PIN, 128);
  }
  else if (humVal > 50) {
    analogWrite(LED_PIN, 64);
  } 
  else {
    digitalWrite(LED_PIN, LOW);
  }
}
```

Cette fonction est responsable de l'allumage de la DEL en fonction de l'humidit√© du sol. Plus le sol est humide, plus la DEL est allum√©e.

Les lignes `analogWrite(LED_PIN, X);` permettent d'allumer la DEL √† la valeur X. La valeur X est une valeur entre 0 et 255. Plus la valeur est grande, plus la DEL est allum√©e.

Si on lit le code une ligne √† la fois, il devient assez facile √† comprendre.

# √âtape 7 : T√©l√©verser le programme
Maintenant que vous avez compris le code, vous pouvez le t√©l√©verser sur la carte √©lectronique.

- Appuyez sur le bouton `V√©rifier` pour v√©rifier le code
- Appuyez sur le bouton `T√©l√©verser` pour t√©l√©verser le code sur la carte √©lectronique
- Attendez que le t√©l√©versement soit compl√©t√©

# √âtape 8 : Observer le fonctionnement
- Vous avez un petit pot de terre avec de plant de tomate
- Enfoncez le capteur dans la terre
- Activez le moniteur s√©rie et le traceur s√©rie
- Observez les valeurs de l'humidit√© du sol
- Vous avez un petit pot d'eau
- Versez un peu d'eau dans le pot
- Observez les valeurs de l'humidit√© du sol
- Continuez √† verser de l'eau et observez les valeurs qui devraient augmenter

# Conclusion
- Vous avez appris √† envoyer un programme sur une carte √©lectronique
- Il s'agit d'un des √©l√©ments que l'on apprend dans le programme des Techniques de l'informatique
- Il y a beaucoup plus que √ßa dans le programme, mais cela donne un ape√ßu de ce que l'on apprend
- En esp√©rant que vous avez appr√©ci√© l'activit√©